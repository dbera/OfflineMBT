/**
 * Copyright (c) 2024, 2025 TNO-ESI
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available
 * under the terms of the MIT License which is available at
 * https://opensource.org/licenses/MIT
 *
 * SPDX-License-Identifier: MIT
 */
module nl.esi.comma.testspecification.GenerateTestspecification

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "nl.esi.comma.testspecification"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "nl.esi.comma.testspecification.Testspecification"
			fileExtensions = "tspec"
			
            referencedResource = "platform:/resource/nl.esi.comma.actions/model/generated/Actions.genmodel"
            referencedResource = "platform:/resource/nl.esi.comma.assertthat/model/generated/AssertThat.genmodel"
			
			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			generator = null
            contentAssist = nl.esi.xtext.lsp.generator.ide.contentassist.IdeContentAssistFragment2 {
                generateXtendStub = true
            }
			junitSupport = {
				junitVersion = "5"
			}
			
			scopeProvider = scoping.ImportNamespacesScopingFragment2 {
				
			}
//			
//			fragment = scoping.ImportNamespacesScopingFragment2 auto-inject {}
//    		fragment = exporting.QualifiedNamesFragment2 auto-inject {}
//    		fragment = builder.BuilderIntegrationFragment2 auto-inject {}
		}
	}
}
