grammar nl.esi.comma.testspecification.Testspecification with nl.esi.comma.actions.Actions // with org.eclipse.xtext.common.Terminals

generate testspecification "http://www.esi.nl/comma/testspecification/Testspecification"

import "http://www.esi.nl/comma/signature/InterfaceSignature" as sig 
import "http://www.esi.nl/comma/types/Types" as types
import "http://www.esi.nl/comma/expressions/Expression" as expr
import "http://www.asml.nl/matala/product/Product" as prod

TSMain:
	imports += Import*
	namespace += NameSpace*
	model = ModelType
;

@Override
NamedElement returns types::NamedElement:
	name=QualifiedName | Variable
;

ModelType:
	APIDefinition | TestDefinition /* concrete */| AbstractTestDefinition | ContextMap
;

APIDefinition:
	('data-instances' vars+=Variable+)
	('data-init' initActions+=(AssignmentAction | RecordFieldAssignmentAction)+)?
;

ContextMap:
	{ContextMap} 'context-map'
	tMap += TMap*
;

AbstractTestDefinition:
	{AbstractTestDefinition} 'abstract-test-definition'
	testSeq += AbstractTestSequence*
	'generate-file' filePath = STRING
;

TestDefinition:
	{TestDefinition}
	('Test-Purpose' purpose = STRING)?
	('Background' background = STRING)?
	('Stakeholders' stakeholder += StakeHolder+)?
	
	testSeq += TestSequence* 
	stepSeq += StepSequence*
	'generate-file' filePath = STRING
	('global-parameters' gparams+=Variable+)?
	('step-parameters' stepparams+=Variable+)?
	('sut-parameters' sutparams+=Variable+)?
	('global-param-init' gparamsInitActions+=(AssignmentAction | RecordFieldAssignmentAction)+)?
	('sut-param-init' sutInitActions+=(AssignmentAction | RecordFieldAssignmentAction)+ )?
;

StakeHolder:
	'stakeholder-name' name = STRING 
	'function' function = STRING 
	'comments' comments = STRING
;

TMap:
	'abstract-step' name = ID ('with')? (mode = STRING)? ('->' stepSeqId = [StepSequence|ID])?
;

/*********************************************************/
AbstractTestSequence:
	'Test-Scenario' ':' name = ID
		// step += [TMap|ID]+
		step += (AbstractStep | AssertStep)+  // | ConstraintStep 
		
;

ConstraintStep:
	'data-references' ':' name = ID
//	ce += ConstraintElm+
	ce += [prod::RefConstraint|QualifiedName]+	
;

AssertStep:
	'output-assertion' ':' name = ID
	ce += ConstraintElm+
;

ConstraintElm:
	name = ID ':' val = STRING
;

AbstractStep:
	ComposeStep | RunStep
;

ComposeStep:
	'compose-step-name' ':' name = ID
	stepRef += StepReference*
	'input-binding' ':' input += Binding+
	'output-data' ':' output += Binding+ (suppress?='suppress'?)
	refs += ConstraintStep*
;

RunStep:
	'run-step-name' ':' name = ID
	('step-type' ':' stepType += STRING)?
	stepRef += StepReference*
	'input-binding' ':' input += Binding+
	'output-data' ':' output += Binding+ (suppress?='suppress'?) // do not generate these in concrete TSpec
;

StepReference:
	'consumes-from-step' ':' refStep = [AbstractStep|ID] '{' refData += [expr::Variable|QualifiedName]+ '}'
;

Binding:
	name = [expr::Variable|QualifiedName] ':' 
		(	strvalue = STRING
		|	'{'
				kvPairs += NestedKeyValuesPair (',' kvPairs += NestedKeyValuesPair)*
			'}'
		)
;

NestedKeyValuesPair:
	actions += Action+
;

/*********************************************************/

TestSequence:
	'test-sequence' name = ID '{'
		stepSeqRef += [StepSequence|ID]+
	'}'
;

StepSequence:
	'step-sequence' name = ID '{'
		step += Step+
	'}'
;


Step:
	//'step-id' identifier = ID
	'step-id' inputVar = [expr::Variable|ID]
	'step-type' type = [types::TypeDecl|ID]
	'step-input' stepVar = [expr::Variable|ID]
	('for-features' featuresForStep += [expr::Variable|ID]+ )?
	refStep += RefStep*
	//('ref-to-step-output' input = ActionList)?
	//('output-data' output = ActionList)?
	//('local-parameters' localParams += Param+)? 
;

RefStep:
	'ref-to-step-output' input = ActionList 	('for-features' featuresToOutput += [expr::Variable|ID]+ )?
;

Param:
	name=ID ':' value += STRING+
;

@Override
ModelContainer returns types::ModelContainer:
	super::ModelContainer | TSMain
;

/*Body:
	'trigger:' event = [sig::InterfaceEvent] data += EventData+
	(tags += TagText*)
;

TagText:
	(command ?= 'command')? 'tag' tag += (EventTag | EventOutcomeTag)+
;

EventTag:
	'action' eventTag = STRING
;

EventOutcomeTag:
	'data' (value += Expression (',' value += Expression)*)? '->' eventOutcomeTag = STRING
;

/*EventData:
	'state:' state = [sm::State] ('select-transition-clause-id:' evtInst += EventInstance+ )? params += EventParams*
;

Tail:
	('non-trigger:' niEvent = [sig::InterfaceEvent])
	(tags += TagText*)
;

EventInstance:
	'(' instanceID = INT ':' '(' evtCl += EventClause+ ')' params += EventParams* ')'
;

EventClause:
	clauseID = INT | ',' clauseID = INT
;

EventParams: {EventParams}
	'params:' '(' (value += Expression (',' value += Expression)*)? ')' ('feature-expr:' config = Expression)?
;*/