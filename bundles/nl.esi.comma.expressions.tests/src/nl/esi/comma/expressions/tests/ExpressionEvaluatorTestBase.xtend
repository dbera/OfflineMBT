/**
 * Copyright (c) 2024, 2025 TNO-ESI
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available
 * under the terms of the MIT License which is available at
 * https://opensource.org/licenses/MIT
 *
 * SPDX-License-Identifier: MIT
 */
/*
 * generated by Xtext 2.36.0
 */
package nl.esi.comma.expressions.tests

import com.google.inject.Inject
import nl.esi.comma.expressions.evaluation.ExpressionEvaluator
import nl.esi.comma.expressions.expression.ExpressionModel
import nl.esi.xtext.common.lang.utilities.EcoreUtil3
import org.eclipse.xtext.resource.SaveOptions
import org.eclipse.xtext.serializer.ISerializer
import org.eclipse.xtext.testing.InjectWith
import org.eclipse.xtext.testing.extensions.InjectionExtension
import org.eclipse.xtext.testing.util.ParseHelper
import org.junit.jupiter.api.Assertions
import org.junit.jupiter.api.^extension.ExtendWith

@ExtendWith(InjectionExtension)
@InjectWith(ExpressionInjectorProvider)
abstract class ExpressionEvaluatorTestBase {
    @Inject
    ParseHelper<ExpressionModel> parser

    @Inject
    ISerializer serializer

    val SAVE_OPTIONS = SaveOptions.newBuilder.format.options

    protected def void assertEval(String expected, String input) {
        val actualFormatted = eval(input)
        val expectedExprs = parser.parse(expected)
        Assertions.assertTrue(expectedExprs.eResource.errors.isEmpty, '''Unexpected errors in expected output: «expectedExprs.eResource.errors.join(", ")»''')
        val expectedFormatted = serializer.serialize(EcoreUtil3.unformat(expectedExprs), SAVE_OPTIONS)
        Assertions.assertEquals(expectedFormatted, actualFormatted)
    }

    protected def String eval(String input) {
        val expressions = parser.parse(input)
        Assertions.assertTrue(expressions.eResource.errors.isEmpty, '''Unexpected errors in input: «expressions.eResource.errors.join(", ")»''')
        Assertions.assertEquals(expressions.variables.size, expressions.variables.map[variable.name].toSet.size, 'Variables cannot be declared multiple times')
        val evaluator = new ExpressionEvaluator()
        val context = expressions.variables.toMap([variable], [expression])
        for (assignment : expressions.variables.reject[expression === null]) {
            assignment.expression = evaluator.evaluate(assignment.expression) [ variable |
                return context.get(variable)
            ]
//            context.put(assignment.variable, assignment.expression)
        }
        return serializer.serialize(EcoreUtil3.unformat(expressions), SAVE_OPTIONS)
    }
}
