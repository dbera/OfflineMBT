/**
 * Copyright (c) 2024, 2025 TNO-ESI
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available
 * under the terms of the MIT License which is available at
 * https://opensource.org/licenses/MIT
 *
 * SPDX-License-Identifier: MIT
 */
grammar nl.asml.matala.product.Product with nl.esi.comma.assertthat.AssertThat

generate product "http://www.asml.nl/matala/product/Product"

import "http://www.esi.nl/xtext/common/lang/Base" as base
import "http://www.esi.nl/comma/types/Types" as types
import "http://www.esi.nl/comma/expressions/Expression" as expr

Product returns base::ModelContainer: {Product}
	imports += Import*
	(namespace += NameSpace)*
    types += TypeDecl*
	( systemDecl = SystemDecl	
	| specification = Specification)
;

@Override
NamedElement returns base::NamedElement:
	name=QualifiedName | SystemDecl
;

SystemDecl:
	'system' 'declarations' name = ID '{'
		block += Block*
		// if it works, make it recursive
	'}'
;

Specification:
	'specification' name = ID '{'
		blocks += Blocks+
		('SUT-blocks' envBlock += [Block|QualifiedName]*)?
		'depth-limits' limit = INT
		('num-tests' numTests = INT)?
	'}'
;

Blocks:
	( block = Block
	| refBlock = RefBlock
	)
;


RefBlock:
	'reference' 'system' system = [Block|QualifiedName]
;

Block:
	'system' name = ID '{'
		( // Unordered group for following items
			('inputs' invars+=Variable* )? &
			('outputs' outvars+=Variable* )? &
			('local' localvars+=Variable* )? &
            ('suts' sutvars += [expr::Variable|ID] (',' sutvars+=[expr::Variable|ID])*)? &
			('init' initActions+=AssignmentAction*)? &
			'desc' type = STRING
		)
		functions += Function*
		('element-labels' '[' elabels += STRING (',' elabels+=STRING+)* ']')?
	'}'
;

Function:
	'action' name = ID
	('element-label'  elabels = STRING )?
	updates += Update*
;

Update:
	'case' name = ID 
	('step-type' stepType = STRING 'action-type' actionType = ActionType)?
	('with-inputs' fnInp += VarRef (',' fnInp+=VarRef)*)?
	('with-guard' guard=Expression?)?
	(dataAssertions = DataAssertions)? 
	updateOutputVar += UpdateOutVar*
;

enum ActionType:
	INTERNAL | COMPOSE | RUN | ASSERT
;

UpdateOutVar:
	'produces-outputs'  fnOut += VarRef (',' fnOut+=VarRef)* (assert?='symbolic-link'?) (suppress=Suppress)?
	('updates' hiddenVars += Variable* ':' act = ActionList)?
;

VarRef:
	opt?='opt'? init?='init'? 
	ref = [expr::Variable|ID] 
	(dataConstraints = DataConstraints)?
	(dataReferences = DataReferences)? 
;

Suppress: {Suppress}
    'suppress' ('(' varFields+=SuppressVarFieldExp (',' varFields+=SuppressVarFieldExp)* ')')?
;

SuppressVarFieldExp returns expr::Expression:
    ExpressionVariable | FieldAccessExp
;

DataConstraints: {DataConstraints}
	'constraints' '{' 
		constr += SymbConstraint*
	 '}'
;

DataReferences: {DataReferences}
	'references' '{' 
		constr += RefConstraint*
	 '}'
;

RefConstraint:
	name=ID ':' act = ActionList
;

SymbConstraint:
	name=ID ':' symbExpr = Expression
;
