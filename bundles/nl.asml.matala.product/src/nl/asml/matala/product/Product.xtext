grammar nl.asml.matala.product.Product with nl.esi.comma.actions.Actions // with org.eclipse.xtext.common.Terminals

generate product "http://www.asml.nl/matala/product/Product"

import "http://www.esi.nl/comma/signature/InterfaceSignature" as sig 
import "http://www.esi.nl/comma/types/Types" as types
import "http://www.esi.nl/comma/expressions/Expression" as expr

Product:
	imports += Import*
	'specification' name = ID '{'
		block += Block*
		('environment-blocks' envBlock += [Block|ID]*)?
		'depth-limits' limit = Int
	'}'
;

@Override
ModelContainer returns types::ModelContainer:
	super::ModelContainer | Product
;

Block:
	'system' name = ID '{'
		('inputs' invars+=Variable+ )?
		('outputs' outvars+=Variable+)?
		('local' localvars+=Variable+ )?
		('init' initActions+=(AssignmentAction | RecordFieldAssignmentAction)+)?
		'desc' type = STRING
		functions += Function*
	'}'
;

Function:
	'action' name=ID  
	updates += Update*
;

Update:
	'case' name = ID 
	('with-inputs' fnInp += VarRef (',' fnInp+=VarRef)*  )?
	('with-guard' guard=Expression)?
	updateOutputVar += UpdateOutVar*
;

UpdateOutVar:
	'produces-outputs'  fnOut += VarRef (',' fnOut+=VarRef)* (assert?='assert'?)
	('updates' hiddenVars += Variable* ':' act = ActionList)?
;

VarRef:
	opt?='opt'? init?='init'? 
	ref = [expr::Variable|ID] 
	(dataConstraints = DataConstraints)? 
;

DataConstraints:
	'constraints' '{' 
		constr += Constraint+
	 '}'
;

Constraint:
	name=ID ':' symbExpr=Expression
;
