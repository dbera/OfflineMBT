/**
 * Copyright (c) 2024, 2025 TNO-ESI
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available
 * under the terms of the MIT License which is available at
 * https://opensource.org/licenses/MIT
 *
 * SPDX-License-Identifier: MIT
 */
/*
 * Language for interface signatures.
 * Signatures define interface events and may define types.
 */
grammar nl.esi.comma.signature.InterfaceSignature with nl.esi.comma.types.Types

generate interfaceSignature "http://www.esi.nl/comma/signature/InterfaceSignature"

import "http://www.esi.nl/xtext/common/lang/Base" as base
import "http://www.esi.nl/comma/types/Types" as types

/*
 * Signature definitions may optionally and transitively import type models
 */
InterfaceSignatureDefinition returns base::ModelContainer: {InterfaceSignatureDefinition}
    imports += Import*
	signature = Signature
;

@Override
NamedElement returns base::NamedElement:
    super::NamedElement | Parameter | Signature
;

Signature:
	'signature' name = ID 
		('types' types += TypeDecl+)?
		("commands" commands += Command+) ?
		('signals' signals+= Signal+) ?
		("notifications" notifications += Notification+) ?
;

Parameter:
	(direction = DIRECTION)? type = Type name = ID
;

enum DIRECTION :
	IN = 'in' |
	OUT = 'out' |
	INOUT = 'inout'
;

InterfaceEvent returns base::NamedElement:
    {InterfaceEvent}
;

ConcreteInterfaceEvent returns InterfaceEvent:
    {Command} | {Signal} | {Notification}
;

/*
 * Commands are synchronous calls from a client to a server.
 * Return type can be void
 */
Command:
	type = Type name = ID ("(" parameters+=Parameter (',' parameters+=Parameter)* ")")?
;

/*
 * Notifications are asynchronous events from a server to its clients
 */
Notification:
	name = ID ("(" parameters+=Parameter (',' parameters+=Parameter)* ")")?
;

/*
 * Signals are asynchronous calls from a client to a server
 */
Signal:
	name = ID ("(" parameters+=Parameter (',' parameters+=Parameter)* ")")?
;
