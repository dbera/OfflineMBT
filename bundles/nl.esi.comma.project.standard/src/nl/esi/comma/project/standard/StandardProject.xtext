/**
 * Copyright (c) 2024, 2025 TNO-ESI
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available
 * under the terms of the MIT License which is available at
 * https://opensource.org/licenses/MIT
 *
 * SPDX-License-Identifier: MIT
 */
grammar nl.esi.comma.project.standard.StandardProject with nl.esi.comma.types.Types

generate standardProject "http://www.esi.nl/comma/project/standard/StandardProject"

Project:
	'Project' name = ID '{'
		(offlineBlocks += OfflineGenerationBlock)*
		(statemachineBlocks += StateMachineGenerationBlock)*
	'}'
;

OfflineGenerationBlock:
	'Generate' target=OfflineGenerationTarget '{'
		name = ID '{'
            (('bpmn-file' bpmn = STRING) | ('product-file' product = STRING))
			('num-tests' numTests = INT)?
			('depth-limit' depthLimit = INT)?
            ('path-to-python-exe' pythonExe = STRING)?
		'}'
	'}'
;

enum OfflineGenerationTarget:
    SIMULATOR='Simulator' | TESTS='Tests' | FAST='FAST';

StateMachineGenerationBlock:
	'Conformance-Coverage-And-Test-Generation' '{'
		name = ID '{'
			'constraints' ('files' constraintsFiles += STRING+ | 'directories' constraintsDirs += FilePath+)
			(visualizeSM ?= 'visualize-constraints')?
			(printConstraints ?= 'print-constraints')?
			(testGen = TestGeneration)?
			('use-scenario-file:' scenarioFile = STRING)?
			(checkCoCo ?= 'check-conformance-and-coverage')?
		'}'
	'}'
;

TestGeneration:
    'generate-test-scenarios' (numSCN = INT)? 'with-strategy' 
            (algorithmPrefixSuffix ?= 'prefix-suffix' 
            | algorithmPrefixSuffixMinimized ?= 'prefix-suffix-minimized'
            | algorithmPrefix ?= 'prefix' 
            | algorithmPrefixMinimized ?= 'prefix-minimized' 
            | algorithmDfs ?= 'dfs' | algorithmBfs ?= 'bfs') ('{' (
                    ('k:' k = INT)? &
                    ('timeout:' timeout = INT)? &
                    ('similarity-threshold(0-100):' similarity = INT)? &
                    (skipDuplicateSelfLoop?='skip-duplicate-self-loop')? &
                    (skipSelfLoop?='skip-self-loop')? &
                    (skipAny?='skip-any')?
                ) '}')?
;

FilePath:
	path = STRING
;
