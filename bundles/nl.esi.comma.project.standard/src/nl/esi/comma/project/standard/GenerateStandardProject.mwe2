module nl.esi.comma.project.standard.GenerateStandardProject

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "nl.esi.comma.project.standard"
				rootPath = rootPath
				runtimeTest = {
					root = "${rootPath}/../tests/nl.esi.comma.project.standard.tests/"
					enabled = false
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					root = "${rootPath}/../tests/nl.esi.comma.project.standard.ui.tests/"
					enabled = false
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "nl.esi.comma.project.standard.StandardProject"
			fileExtensions = "prj"
			
			referencedResource = "platform:/resource/nl.esi.comma.signature/model/generated/InterfaceSignature.genmodel"
			

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			formatter = formatting.Formatter2Fragment2 {				
				generateStub =true		
			}
            contentAssist = nl.esi.xtext.lsp.generator.ide.contentassist.IdeContentAssistFragment2 {
                generateXtendStub = true
            }
			generator = {
				generateXtendMain = true
			}
		}
	}
}
