/**
 * Copyright (c) 2024, 2025 TNO-ESI
 *
 * See the NOTICE file(s) distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This program and the accompanying materials are made available
 * under the terms of the MIT License which is available at
 * https://opensource.org/licenses/MIT
 *
 * SPDX-License-Identifier: MIT
 */
/*
 * generated by Xtext 2.36.0
 */
package nl.esi.xtext.common.lang.validation

import nl.esi.xtext.common.lang.base.BaseModel
import nl.esi.xtext.common.lang.base.BasePackage
import nl.esi.xtext.common.lang.base.Import
import nl.esi.xtext.common.lang.base.NamedElement
import nl.esi.xtext.common.lang.utilities.EcoreUtil3
import org.eclipse.emf.ecore.EClass
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.validation.Check

/**
 * This class contains custom validation rules. 
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class BaseValidator extends AbstractBaseValidator {

    /**
     * Constrains:
     * - imports are valid URIs
     * - imported resources contain valid types, see {@link #isValidImportType(EClass)}
     */
    @Check
    def checkImportForValidity(Import imp) {
        if (! EcoreUtil3.isValidUri(imp)) {
            error('Invalid resource', imp, BasePackage.Literals.IMPORT__IMPORT_URI)
        } else {
            val Resource res = EcoreUtil3.getResource(imp.eResource, imp.importURI)
            if (! res.contents.forall[isValidImportType(eClass)]) {
                error('Imported type is not allowed', imp, BasePackage.Literals.IMPORT__IMPORT_URI)
            }
        }
    }

    protected def boolean isValidImportType(EClass importType) {
        return BasePackage.Literals.MODEL_CONTAINER.isSuperTypeOf(importType)
    }

    @Check
    def checkBaseModelElementsDuplicateName(BaseModel model) {
        model.elements.checkForNameDuplicationsVerbose('element', '', null)
    }

    // Generic method to detect name duplications in a collection of a named elements
    protected def checkForNameDuplications(Iterable<? extends NamedElement> elements, String elementDescription, String code, String... issueData) {
        elements.checkForNameDuplicationsVerbose(elementDescription, null, code, issueData)
    }

    protected def checkForNameDuplicationsVerbose(Iterable<? extends NamedElement> elements, String elementDescription, String namePrefix, String code, String... issueData) {
        val duplicates = elements.groupBy[name].values.filter[size > 1].flatten
        for (duplicate : duplicates) {
            var errorText = '''Duplicate «elementDescription» name«IF namePrefix !== null»: «namePrefix»«duplicate.name»«ENDIF»'''
            error(errorText, duplicate, BasePackage.Literals.NAMED_ELEMENT__NAME, code, issueData)
        }
    }
}
